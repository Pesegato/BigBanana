plugins {
		id 'java-library'
		id 'maven-publish'
		id "idea"
		id 'signing'
		id 'com.palantir.git-version' version '3.0.0'
}

group 'io.github.pesegato'
version gitVersion()

println("version : '${version}'")

ext {
		jvm = org.gradle.internal.jvm.Jvm.current()
		javaVersion = JavaVersion.current()
		isCiServer = System.getenv("TRAVIS") || System.getenv().containsKey("TEAMCITY_VERSION") || hasProperty('CI') || System.getenv("JITPACK")
		isWindows = org.gradle.internal.os.OperatingSystem.current().windows
}

repositories {
		mavenLocal()
		mavenCentral()
}

def jme3 = ["v": '3.6.1-stable', "g": "org.jmonkeyengine"]
/*
def pesegato = ["g" :"com.pesegato" ,"v":"latest.integration"]
if (isCiServer){
	pesegato.g="com.github.Pesegato"
	pesegato.v="-SNAPSHOT"
}
*/

dependencies {
		// jme
	implementation "org.jmonkeyengine:jme3-desktop:${jme3.v}"
		implementation "org.jmonkeyengine:jme3-lwjgl3:${jme3.v}"
		implementation 'org.slf4j:slf4j-api:2.0.9'
		//implementation 'com.simsilica:lemur:1.16.1-PESEGATO'
	implementation 'com.simsilica:lemur:1.16.0'
	implementation 'com.simsilica:lemur-proto:1.13.0'
		//implementation 'com.simsilica:lemur:1.13.0'
		implementation 'com.simsilica:sio2:1.8.0'
		implementation 'com.google.guava:guava:32.1.3-jre'
		runtimeOnly 'ch.qos.logback:logback-classic:1.3.13'
		runtimeOnly "${jme3.g}:jme3-lwjgl3:${jme3.v}"

		//utils

		//assets

		//test
}

signing {
	sign publishing.publications
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			pom {
				name = 'BigBanana'
				description = 'Mouseless (bananaful?) navigation for jme3 applications'
				url = 'https://github.com/Pesegato/BigBanana/'
				inceptionYear = '2016'
				licenses {
					license {
						name = 'BSD 3-Clause License'
						url = 'https://github.com/Pesegato/BigBanana/blob/main/LICENSE'
					}
				}
				developers {
					developer {
						id = 'pesegato'
						name = 'Pesegato Ameiurus Melas'
						email = 'pesegato@outlook.com'
					}
				}
				scm {
					connection = 'scm:git:git://github.com/pesegato/bigbanana.git'
					developerConnection = 'scm:git:ssh://git@github.com/pesegato/bigbanana.git'
					url = 'https://github.com/Pesegato/BigBanana/'
				}
			}
		}
	}
	repositories {
		/*
		maven {
			url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
			credentials {
				username = ossrhUsername
				password = ossrhPassword
			}
		}
		*/
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/pesegato/bigbanana"
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(11)
	}
	withJavadocJar()
	withSourcesJar()
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

compileJava.doLast {
		def buildDate = new Date().format('yyyy-MM-dd HHmm')
		println "Writing ${rootProject.name}.build.date: " + buildDate
		destinationDirectory.file("${rootProject.name}.build.date").get().asFile.text = buildDate
		destinationDirectory.file("${rootProject.name}.build.version").get().asFile.text = "${version}"
}
